plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

forgeVersion = "1.20.2"
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.20.2'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.2-48.1.0'

    implementation 'pixelmon:Pixelmon-1.20.2-9.2.6-server:9.2.6'
    shadow group: 'org.spongepowered', name: 'configurate-yaml', version: '4.2.0-COMMENTS'

    shadow (group: 'com.envyful.api', name: 'commons', version: '6.1.2')
    shadow (group: 'com.envyful.api', name: 'forge20', version: '6.1.2') {
        transitive = false
    }
    shadow (group: 'com.envyful.api', name: 'reforged20', version: '6.1.2') {
        transitive = false
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    setArchiveClassifier('')
    setArchiveBaseName("${rootProject.name}-Forge")

    relocate('org.spongepowered.configurate', 'com.envyful.battle.enforcement.shade.configurate')
    relocate('org.yaml.snakeyaml', 'com.envyful.battle.enforcement.shade.snakeyaml')
    relocate('io.leangen.geantyref', 'com.envyful.battle.enforcement.shade.geantyref')
    relocate('com.zaxxer', 'com.envyful.battle.enforcement.shade.hikari')
    relocate('org.slf4j', 'com.envyful.battle.enforcement.shade.slf4j')
    relocate('com.envyful.api', 'com.envyful.battle.enforcement.shade.envy.api')
    relocate('org.bstats', 'com.envyful.battle.enforcement.shade.bstats')
    relocate('io.netty', 'com.envyful.battle.enforcement.shade.netty')
    relocate('io.lettuce', 'com.envyful.battle.enforcement.shade.lettuce')
    relocate('com.cronutils', 'com.envyful.battle.enforcement.shade.cronutils')
    relocate('com.sun.el', 'com.envyful.battle.enforcement.shade.sun.el')
    relocate('org.reactivestreams', 'com.envyful.battle.enforcement.shade.reactivestreams')
    relocate('reactor', 'com.envyful.battle.enforcement.shade.reactor')
    relocate('com.mysql', 'com.envyful.battle.enforcement.shade.mysql')

    exclude 'org/apache/logging/**'
    exclude "com/google/**"
    exclude "javax/**"
    exclude "**/module-info.class"
    exclude "META-INF/versions/9/**"
    exclude "META-INF/native-image/**"
    exclude "META-INF/services/**"
}

jar.finalizedBy('shadowJar')
shadowJar.finalizedBy('reobfJar')
build.finalizedBy('versionedRelease')
